; rs-liveries.nsi.j2

;--------------------------------
;Include Modern UI
!include MUI2.nsh
; LogicLib is a requirement for nsDialogs (I think...)
!include LogicLib.nsh
!include nsDialogs.nsh
; Get powershell output in install log. Source: https://nsis.sourceforge.io/PowerShell_support
!include psexec.nsh
!define MUI_COMPONENTSPAGE_NODESC
!define MUI_ICON rs.ico
Icon "rs.ico"

;--------------------------------

;--------------------------------

; This might help with large installs
FileBufSize 256

; Let's compress as much as possible. /GLOBAL breaks the installer. Too many big files!
SetCompressor /FINAL zlib
; SetCompressor /FINAL lzma

; LZMA compression only
; SetCompressorDictSize 128


; The name of the installer
Name "RS Liveries"
VIProductVersion ${VERSION}

; The file to write
OutFile "RS Liveries.exe"

; Request application privileges for Windows Vista
RequestExecutionLevel admin

; Build Unicode installer
Unicode True

Var Dialog
Var Label
Var LabelBrowse
Var Pilot
Var PilotSelected
Var ProgramFilesDir
Var BrowseButton


Function nsDialogsPage
	nsDialogs::Create 1018
	Pop $Dialog

	${If} "$Dialog" == error
		Abort
	${EndIf}

	${NSD_CreateLabel} 0 0 100% 12u "Who are you?"
	Pop $Label
	
  ${NSD_CreateDropList} 0 20 100% 12u ""
	Pop $Pilot
  ${NSD_CB_AddString} $Pilot "-- RS SQUAD BUT NOT IN LIST --"
  ${NSD_CB_AddString} $Pilot "-- STREAMER / YOUTUBER --"
  {%- for pilot in pilots %}
  ${NSD_CB_AddString} $Pilot "{{ pilot }}"
  {%- endfor %}
  ${NSD_CB_SelectString} $Pilot "-- RS SQUAD BUT NOT IN LIST --"
	
  ${NSD_CreateLabel} 0 60 100% 12u "Please select the install directory of DCS"
	Pop $LabelBrowse
  
  ${NSD_CreateBrowseButton} 0 80 100% 12u "$ProgramFilesDir"
  Pop $BrowseButton
  ${NSD_OnClick} $BrowseButton OnDirBrowse

	nsDialogs::Show
FunctionEnd

Function OnDirBrowse
    ${NSD_GetText} $BrowseButton $0
    nsDialogs::SelectFolderDialog "Please select the install directory of DCS" "$0" 
    Pop $0
    ${If} $0 != error
        ${NSD_SetText} $BrowseButton "$0"
    ${EndIf}
FunctionEnd

Function nsDialogsPageLeave
	${NSD_GetText} $Pilot $PilotSelected
  ${NSD_GetText} $BrowseButton $ProgramFilesDir
FunctionEnd

Function .onInit
  # Detect which Save/liveries directory to use. If multiple exist, use the most bottom one from this list.
  ${If} ${FileExists} "$PROFILE\Saved Games\DCS.openbeta"
    StrCpy $InstDir "$PROFILE\Saved Games\DCS.openbeta\Liveries"
  ${ElseIf} ${FileExists} "$PROFILE\Saved Games\DCS"
    StrCpy $InstDir "$PROFILE\Saved Games\DCS\Liveries"
  ${EndIf}
  
  ReadRegStr $ProgramFilesDir "HKCU" "SOFTWARE\Eagle Dynamics\DCS World OpenBeta" "Path"
  ${If} "$ProgramFilesDir" == ""
    ReadRegStr $ProgramFilesDir "HKCU" "SOFTWARE\Eagle Dynamics\DCS World" "Path"
  ${EndIf}

  InitPluginsDir
  File "/oname=$PluginsDir\mig29flyby.wav"                   mig29flyby.wav
  File "/oname=$PluginsDir\rssplash.bmp"                     rssplash.bmp
  File "/oname=$PluginsDir\7za.exe"                          7za.exe
  File "/oname=$PluginsDir\rs-liveries-pilot-priorities.ps1" rs-liveries-pilot-priorities.ps1
  File "/oname=$PluginsDir\extract-file.ps1"                 extract-file.ps1
  newadvsplash::play "$PluginsDir\mig29flyby.wav"
  newadvsplash::show 8200 1000 1000 "0x333333" /PASSIVE $PluginsDir\rssplash.bmp
  ; If there is ever a problem with the newadvsplash download, use the old advsplash plugin
  ; The old plugin does not have working fade-in and fade-out effects
  ; advsplash::show 1000 600 400 "0x333333" $PluginsDir\rssplash
FunctionEnd

;--------------------------------

!insertmacro MUI_PAGE_DIRECTORY
Page custom nsDialogsPage nsDialogsPageLeave
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"


!macro DownloadExtract URL ArchivePath ExtractPath BaseName
  ; Basename is the filename without extension
  SetOutPath "$PluginsDir"
  File "RED STAR BIN\${BaseName}.sha256sum"
  ${If} ${FileExists} "$InstDir\RED STAR BIN\${BaseName}.sha256sum"
    DetailPrint "Checking whether to reinstall ${BaseName} ..."
    FileOpen $9 "$PluginsDir\${BaseName}.sha256sum" r
    FileOpen $R9 "$InstDir\RED STAR BIN\${BaseName}.sha256sum" r
    FileRead $9 $8
    FileRead $R9 $R8
    FileClose $9
    FileClose $R9
    DetailPrint $R8
    DetailPrint $8
    ${If} "$8" == "$R8"
      DetailPrint "${BaseName} already installed. Skipping. Use Cleanup to reinstall."
      Goto SkipToEnd
    ${Else}
      DetailPrint "${BaseName} differs from the current version. Installing"
    ${Endif}
  ${EndIf}
  DetailPrint "Downloading ${URL}.7z to ${ArchivePath}"
  NScurl::http GET "${URL}.7z" "${ArchivePath}" /INSIST /POPUP /END
  Pop $0 ; Status text ("OK" for success)
  ${If} "$0" != "OK"
    Abort "Download of ${URL}.7z failed. Aborting install. Please ensure you're running the latest release."
  ${EndIf}
  ClearErrors
  DetailPrint "Extracting ${ArchivePath} to ${ExtractPath}"
  ${PowerShellExecFileLog}  '"$PluginsDir\extract-file.ps1" -7a_exec_path "$PluginsDir\7za.exe" -destination_dir "${ExtractPath}" -archive_path "${ArchivePath}"  '
  IfErrors 0 +2
    Abort "Problem extracting ${ArchivePath} to ${ExtractPath}"
  Delete "${ArchivePath}"
  Delete "$InstDir\RED STAR BIN\${BaseName}.sha256sum"
  CreateDirectory "$InstDir\RED STAR BIN"
  Rename "$PluginsDir\${BaseName}.sha256sum" "$InstDir\RED STAR BIN\${BaseName}.sha256sum"
  SkipToEnd:
!macroend
;--------------------------------

; Old and unused livery folder names are OK to remain in the cleanup list. We want to reduce clutter for the user


SectionGroup "Clean Up Old Red Star Liveries"
  Section /o "Red Star Bin"
    RMDir /r "$INSTDIR\RED STAR BIN"
  SectionEnd
  SectionGroup "RoughMets"
  {%- for roughmet in roughmets %}
    Section /o "{{ roughmet['roughmet_directory_basename'] }}"
    {%- for roughmet_filename in roughmet['files'] %}
      Delete "$ProgramFilesDir\Bazar\TempTextures\{{ roughmet_filename }}"
    {%- endfor %}
    SectionEnd
  {%- endfor %}
  SectionGroupEnd
  Section /o "Red Star Camo Liveries"
  {%- for livery in rs_liveries %}
    {%- include 'rs-liveries.nsi_removal.j2' %}
  {%- endfor %}
  SectionEnd
  Section /o "Red Star Competitive Liveries"
  {%- for livery in rsc_liveries %}
    {%- include 'rs-liveries.nsi_removal.j2' %}
  {%- endfor %}
  SectionEnd
SectionGroupEnd

SectionGroup "!Red Star Required Assets"
  Section "Red Star Bin (shared) Directory"
    AddSize {{ size_bin_kb }}
    !insertmacro DownloadExtract "https://github.com/red-star-squadron/rs-liveries/releases/download/{{ gh_ref }}/RED.STAR.BIN" "$TEMP\RED STAR BIN.7z" "$INSTDIR" "RED STAR BIN"
  SectionEnd
  SectionGroup "Red Star RoughMets"
  {%- for roughmet in roughmets %}
    Section "{{ roughmet['roughmet_directory_basename'] }}"
      AddSize {{ roughmet['size'] }}
      !insertmacro DownloadExtract "https://github.com/red-star-squadron/rs-liveries/releases/download/{{ gh_ref }}/RED.STAR.ROUGHMETS_{{ roughmet['roughmet_directory_basename'] | replace(" ", ".") }}" "$TEMP\{{ roughmet['roughmet_directory_basename'] }}.7z" "$ProgramFilesDir\Bazar\TempTextures" "{{ roughmet['roughmet_directory_basename'] }}"
    SectionEnd
  {%- endfor %}
  SectionGroupEnd
SectionGroupEnd

SectionGroup "Red Star"
  {%- for livery in rs_liveries %}
    {%- include 'rs-liveries.nsi_livery_section.j2' %}
  {%- endfor %}
SectionGroupEnd

SectionGroup "Red Star BLACK SQUADRON"
  {%- for livery in rsc_liveries %}
  {%- include 'rs-liveries.nsi_livery_section.j2' %}
  {%- endfor %}
SectionGroupEnd

Section "Modify Pilot Priority"
  SetOutPath "$PLUGINSDIR\NSISTemp"
  ${PowerShellExecFileLog} '"$PLUGINSDIR\rs-liveries-pilot-priorities.ps1" -dcs_liveries_root_dir "$InstDir" -pilot "$PilotSelected"  '
  IfErrors 0 +2
    Abort "There was a problem with the pilot priorities script. Aborting. You can probably use the liveries as normal."
  ; nsExec::ExecToStack 'powershell -inputformat none -ExecutionPolicy RemoteSigned -File "$PLUGINSDIR\rs-liveries-pilot-priorities.ps1" -dcs_liveries_root_dir "$InstDir" -pilot "$PilotSelected"  '
  ; MessageBox MB_OK "$PilotSelected"
SectionEnd
